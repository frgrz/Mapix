apply plugin: 'maven'
apply plugin: 'signing'

project.ext.set("localMavenRepositoryPath", "file:///Users/francois.grzybowski/.m2/repository")

def isReleaseBuild() {
    return !getVersionName().contains("SNAPSHOT")
}

def getVersionName() {
    if (System.getenv().containsKey('VERSION_NAME_EXTRAS')) {
        return VERSION_NAME + System.getenv('VERSION_NAME_EXTRAS')
    } else {
        return VERSION_NAME
    }
}

def getReleaseRepositoryUrl() {
    // https://bintray.com/api/v1/maven/{NEXUS_USERNAME}/maven/{POM_ARTIFACT_ID}/;publish=1
    return "https://bintray.com/api/v1/maven/" +
            getRepositoryUsername() +
            "/maven/" +
            (hasProperty('POM_ARTIFACT_URL') ? POM_ARTIFACT_URL : POM_ARTIFACT_ID) +
            "/;publish=1"
}

def getSnapshotRepositoryUrl() {
    return "http://oss.jfrog.org/artifactory/oss-snapshot-local"
}

def getRepositoryUsername() {
    if (System.getenv().containsKey('NEXUS_USERNAME')) return System.getenv('NEXUS_USERNAME')
    else return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    if (System.getenv().containsKey('NEXUS_PASSWORD')) return System.getenv('NEXUS_PASSWORD')
    else return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = getVersionName()

                repository(url: getReleaseRepositoryUrl()) {
                    authentication(
                            userName: getRepositoryUsername(),
                            password: getRepositoryPassword()
                    )
                }
                snapshotRepository(url: getSnapshotRepositoryUrl()) {
                    authentication(
                            userName: getRepositoryUsername(),
                            password: getRepositoryPassword()
                    )
                }

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task installArchives(type: Upload) {
        description "Installs the artifacts to the local Maven repository."
        configuration = configurations['archives']
        repositories {
            mavenDeployer {
                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME
                repository url: "file://${System.properties['user.home']}/.m2/repository"
            }
        }
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    task androidJavadocsJar(type: Jar, dependsOn: dokka) {
        getArchiveClassifier().set('javadoc')
        from dokka.outputDirectory
    }

    task androidSourcesJar(type: Jar) {
        getArchiveClassifier().set('sources')
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
